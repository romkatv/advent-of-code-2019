#!/usr/bin/env zsh
#
# Usage: ./solve [task]...

emulate -L zsh
setopt no_aliases err_exit no_unset extended_glob pipe_fail warn_create_global
setopt prompt_percent no_prompt_subst pushd_silent monitor no_hup no_check_jobs

(( ARGC )) || set -- ${ZSH_SCRIPT:h}/<->(/:t)

local task pids=()
trap '(( ! $#pids )) || kill -- -$^pids 2>/dev/null' INT TERM EXIT

for task; do
  [[ $task == <-> ]]
  {
    (
      cd ${ZSH_SCRIPT:h}/$task
      if (( ARGC > 1 )); then
        ./solve <input >answer 2>stderr.log
      else
        ./solve <input 3>&2 >answer 2>&3 2>stderr.log
      fi
    ) &
  } >/dev/null
  pids+=$!
done

for task; do
  print -rP -- "Solving %B$task%b..." >&2
  pushd ${ZSH_SCRIPT:h}/$task
  local -i ret=0
  wait $pids[1] || ret=$?
  kill -- -$pids[1] 2>/dev/null || true
  shift 1 pids
  (( ARGC == 1 )) || <stderr.log >&2
  (( !ret )) || return ret
  print -rP -- "Answer for %B$task%b: %F{green}${(V)$(<answer)//\%//%%}%f" >&2
  popd
done
