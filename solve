#!/usr/bin/env zsh

emulate -L zsh
setopt no_aliases err_exit no_unset extended_glob pipe_fail warn_create_global
setopt prompt_percent no_prompt_subst pushd_silent

(( ARGC )) || set -- ${ZSH_SCRIPT:h}/<->(/:t)

local task

if (( ARGC > 1 )); then
  setopt monitor no_hup no_check_jobs
  local -a pids
  trap '(( ! $#pids )) || kill -- -$^pids 2>/dev/null' INT TERM EXIT
  for task; do
    [[ $task == <-> ]]
    { (cd ${ZSH_SCRIPT:h}/$task && ./solve <input >answer 2>stderr.log) & } >/dev/null
    pids+=$!
  done
fi

for task; do
  [[ $task == <-> ]]
  print -rP -- "Solving %B$task%b..." >&2
  pushd ${ZSH_SCRIPT:h}/$task
  if (( ARGC > 1 )); then
    local -i ret=0
    wait $pids[1] || ret=$?
    shift 1 pids
    <stderr.log >&2
    (( !ret )) || return ret
  else
    ./solve <input 3>&2 >answer 2>&3 2>stderr.log
  fi
  print -rP -- "Answer for %B$task%b: %F{green}${(V)$(<answer)//\%//%%}%f" >&2
  popd
done
