#!/usr/bin/env zsh

emulate -L zsh -o err_return -o no_unset

zmodload zsh/zpty

read program

function amp() (
  { echo -E - $1; exec cat } | { ./icc -c "$program"; exit }
)

integer a b c d e res=-1
range=({5..9})

for a in $range; do
  for b in ${range:#($a)}; do
    for c in ${range:#($a|$b)}; do
      for d in ${range:#($a|$b|$c)}; do
        for e in ${range:#($a|$b|$c|$d)}; do
          zpty z "amp $a | amp $b | amp $c | amp $d | amp $e"
          zpty -w z 0
          integer last=-1
          while zpty -r z line; do
            last=${line%%[[:space:]]*}
            (( last >= 0 ))
            zpty -w z $last
          done
          zpty -d z
          (( last >= 0 ))
          (( res > last )) || res=last
        done
      done
    done
  done
done

(( res >= 0 ))
echo -E - $res
