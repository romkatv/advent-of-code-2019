#!/usr/bin/env zsh

emulate -L zsh -o err_return -o no_unset

zmodload zsh/zpty

read program

function amp() (
  { echo -E - $1; exec cat } | { ./icc -c "$program"; exit }
)

function loop() {
  zpty z $1
  reply=
  local line
  while zpty -r z line; do
    reply=${line%$'\r\n'}
    zpty -w z $reply
  done
  zpty -d z
}

integer a b c d e res=-1
range=({5..9})

for a in $range; do
  for b in ${range:#($a)}; do
    for c in ${range:#($a|$b)}; do
      for d in ${range:#($a|$b|$c)}; do
        for e in ${range:#($a|$b|$c|$d)}; do
          loop "amp '$a 0' | amp $b | amp $c | amp $d | amp $e"
          (( reply >= 0 ))
          (( res > reply )) || res=reply
        done
      done
    done
  done
done

(( res >= 0 ))
echo -E - $res
