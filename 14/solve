#!/usr/bin/env zsh

emulate -L zsh -o err_return -o no_unset

zmodload zsh/zpty

read program

function amp() (
  { echo -E - $1; exec cat } | { ./icc -c "$program"; kill ${sysparams[pid]} }
)

integer a b c d e res=-1
local range=({5..9})

for a in $range; do
  for b in ${range:#($a)}; do
    for c in ${range:#($a|$b)}; do
      for d in ${range:#($a|$b|$c)}; do
        for e in ${range:#($a|$b|$c|$d)}; do
          zpty z "{amp $a | amp $b | amp $c | amp $d | amp $e} 2>/dev/null"
          zpty -w z 0
          integer last=-1
          while zpty -r z line; do
            line=${line%%[[:space:]]*}
            zpty -w z $line
            last=line
          done
          zpty -d z
          (( last >= 0 ))
          (( res > last )) || res=last
        done
      done
    done
  done
done

echo -E - $res
