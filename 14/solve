#!/usr/bin/env zsh

emulate -L zsh
setopt no_aliases err_exit no_unset extended_glob pipe_fail warn_create_global

zmodload zsh/zpty

local program
read -r program

function amp() (
  { echo -E - $1; exec cat } | { ./icc $program; exit }
)

function loop() {
  local REPLY line
  zpty x "( $1 ) 2>&3 && echo ok" 3>&2
  trap 'zpty -d x' EXIT
  zpty -w x 0
  while true; do
    zpty -r x line
    line=${line%$'\r\n'}
    [[ ! -o xtrace || $line != '+(zpty):'* ]] || continue
    [[ $line != ok ]] || break
    [[ $line == <1-> ]]
    zpty -w x $line
    echo -E - $line
  done
}

local -i res
local out x

for x in {56789..98765}; do
  [[ ${(j::)${(os::)x}} == 56789 ]] || continue
  out=${$(loop "amp $x[1] | amp $x[2] | amp $x[3] | amp $x[4] | amp $x[5]")[-1]}
  [[ $out == <1-> ]]
  (( res > out )) || res=out
done

(( res > 0 ))
echo -E - $res
